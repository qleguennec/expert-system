<S> = comments facts truth outp

<comments>  = <comment>*
<facts> = fact*
fact = exp (eq | imp) exp <comment>
<truth> = <sp> <'='> <sp> #'[A-Z]'* <comment>
<outp> = <sp> <'?'> <sp> #'[A-Z]'* <comment>

<exp> = nexp | sexp
pexp = <opar> exp <cpar>
oexp = exp (xor / or / and / imp / eq) exp
<sexp> = <sp> (pexp / oexp / T)
nexp = <sp> <not> (nexp | (pexp / oexp / T))

T = <sp> #'[A-Z]'
not = <sp> <'!'>
and = <sp> <'+'>
or = <sp> <'|'>
xor = <sp> <'^'>
imp = <sp> <'=>'>
eq = <sp> <'<=>'>

sp = #'\s*'
comment = <sp> ('#' #'.*' '\n')?
opar = <sp> <'('>
cpar = <sp> <')'>